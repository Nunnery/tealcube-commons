plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "java"
    id "maven-publish"
    id "wrapper"
}

group "com.tealcube.java.games"
version "0.0.1-SNAPSHOT"

sourceCompatibility = 1.6

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

license {
    header file('HEADER')
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = "commons"
            from components.java
        }
    }
    repositories {
        maven {
            url "http://maven.tealcube.com/artifactory/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "TealCube"
            credentials {
                username System.getenv("ossrhUsername") ?: ossrhUsername
                password System.getenv("ossrhPassword") ?: ossrhPassword
            }
        }
    }
}
